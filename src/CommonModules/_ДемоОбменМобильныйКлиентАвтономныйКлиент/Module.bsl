#Область ПрограммныйИнтерфейс

// Функция проверяет завершение синхронизации основного и автономного серверов
//
// Возвращаемое значение:
//  Булево - Истина, если синхронизация не завершена
Функция ВыполняетсяСинхронизация() Экспорт
	ТекстОшибки = "";
	ИдентификаторФоновогоЗадания = ПараметрыПриложения.Получить("ИдентификаторФоновогоЗадания");
	Возврат ИдентификаторФоновогоЗадания <> Неопределено
		И НЕ _ДемоОбменМобильныйКлиентАвтономныйВызовСервера.ОбменДаннымиЗакончен(ИдентификаторФоновогоЗадания, ТекстОшибки);
КонецФункции

// Процедура выполняет синхронизацию данных
// - запрашивает у пользователя пароль доступа к основной ИБ
// - запускает фоновый процесс синхронизации
// - если не удалось запустить фоновое задание, получает текст ошибки и сообщает пользователю
Процедура НачатьСинхронизацию() Экспорт
	
#Если МобильныйКлиент Тогда
	Если НЕ ВыполняетсяСинхронизация() Тогда
		Если ОсновнойСерверДоступен() = Истина Тогда
			ИдентификаторФоновогоЗадания = _ДемоОбменМобильныйКлиентАвтономныйВызовСервера.ВыполнитьОбменДаннымиВФоне();
			ПараметрыПриложения.Вставить("ИдентификаторФоновогоЗадания", ИдентификаторФоновогоЗадания);
			Оповестить("НачаласьСинхронизация");
			ПодключитьОбработчикОжидания("ОжидатьЗавершениеСинхронизации", 2);
		Иначе
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = НСтр(
				"ru = 'Синхронизация не выполняется в связи с отсутствием связи с основным сервером и возобновится после восстановления связи.'");
			Сообщение.Сообщить();
		КонецЕсли;
	КонецЕсли;
#КонецЕсли
	
КонецПроцедуры

#КонецОбласти